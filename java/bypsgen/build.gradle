import org.gradle.plugins.ide.eclipse.model.AccessRule

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

sourceCompatibility = 11
compileJava.options.encoding = 'UTF-8'
version = versionFromProperties

eclipse {
  classpath {
    file {
      whenMerged {
        def jre = entries.find { it.path.contains 'org.eclipse.jdt.launching.JRE_CONTAINER' }
        jre.accessRules.add(new AccessRule('0', 'jdk/javadoc/**'))
      }
    }
  }
}

sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDir 'res'
        }
    }
}


jar {
    archiveName = "bypsgen.jar"

    manifest {
        attributes 'Implementation-Title': 'BYPS generatore',
                   'Implementation-Version': version,
                   'Main-Class': 'byps.gen.Main'
    }
 
    from {
//        configurations.compileOnly.collect { it.isDirectory() ? it : zipTree(it) }
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

repositories {
    mavenCentral()
}

dependencies {
	compile project(":byps-api")
	compile project(":byps-override")
	compile project(":byps")
	
	// https://mvnrepository.com/artifact/org.glassfish.jaxb/jaxb-runtime
	compile group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: '2.3.1'
	
  compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
  compile group: 'org.slf4j', name: 'jcl-over-slf4j', version: '1.7.30'
  
	compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.10'

	// https://mvnrepository.com/artifact/io.swagger.core.v3/swagger-models
	compile group: 'io.swagger.core.v3', name: 'swagger-models', version: '2.1.7'

	compile group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
}

publishing {
  publications {
     maven(MavenPublication) {
       groupId 'byps'
       artifact jar
    }
  }

  repositories {
       // put the credentials into ~/.gradle/gradle.properties
      def mavenUrl = version.endsWith("SNAPSHOT") ? mavenUploadUrlSnapshots : mavenUploadUrl
  
    maven {
      url mavenUrl
      credentials { 
        username mavenUser 
        password mavenPassword 
      }
    }
  }
}

